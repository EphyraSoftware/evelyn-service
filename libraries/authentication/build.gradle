plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    jcenter()
}

ext {
    keycloakSpringVersion = '4.0.0.Final'
    springBootVersion = '2.1.8.RELEASE'
}

group = 'org.evelyn.library'

dependencies {
    // https://mvnrepository.com/artifact/org.keycloak/keycloak-spring-boot-2-starter
    compile group: 'org.keycloak', name: 'keycloak-spring-boot-2-starter', version: "${keycloakSpringVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-security
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: "${springBootVersion}"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/EphyraSoftware/evelyn-service")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            version = 'dev-local' // Publish a rolling dev-local version
            from components.java
        }
        gpr(MavenPublication) {
            version = 'dev' // Publish a rolling dev version
            from(components.java)
        }
    }
}

task localPublish {
    group = 'evelyn'
    dependsOn publishToMavenLocal
}

task gitHubPublish {
    group = 'evelyn'
    dependsOn publishGprPublicationToGitHubPackagesRepository
}

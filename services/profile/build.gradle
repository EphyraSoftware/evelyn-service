subprojects {
    apply plugin: 'java'

    repositories {
        jcenter()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/EphyraSoftware/evelyn-service")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
        mavenLocal()
    }
}

project.version = '1.0.0'

ext {
    springFrameworkVersion = '5.1.9.RELEASE'
    springBootVersion = '2.1.8.RELEASE'
    springMongoVersion = '2.2.0.RELEASE'
    springRabbitVersion = '2.1.11.RELEASE'
    lombokVersion = '1.18.10'

    libraryAuthenticationVersion = 'dev-local'
    libraryMessageModelsVersion = 'dev-local'
    libraryRemoteConfigurationVersion = 'dev-local'
}

project(":evelyn-service") {
    dependencies {
        compile group: 'org.evelyn.library', name: 'message-models', version: "${libraryMessageModelsVersion}"

        // https://mvnrepository.com/artifact/org.springframework.data/spring-data-mongodb
        compile group: 'org.springframework.data', name: 'spring-data-mongodb', version: "${springMongoVersion}"

        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        compileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"

        // https://mvnrepository.com/artifact/org.springframework.amqp/spring-rabbit
        compile group: 'org.springframework.amqp', name: 'spring-rabbit', version: "${springRabbitVersion}"

        // https://mvnrepository.com/artifact/org.springframework/spring-context-support
        compile group: 'org.springframework', name: 'spring-context-support', version: "${springFrameworkVersion}"

        // https://mvnrepository.com/artifact/org.freemarker/freemarker
        compile group: 'org.freemarker', name: 'freemarker', version: '2.3.27-incubating'
    }
}

project(':rest-api') {
    apply plugin: 'maven-publish'

    group = 'org.evelyn.service'

    dependencies {
        compile project(':evelyn-service')

        // https://mvnrepository.com/artifact/org.springframework/spring-webmvc
        compileOnly group: 'org.springframework', name: 'spring-webmvc', version: "${springFrameworkVersion}"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = 'profile-service-rest-api'
                version = 'dev-local' // Publish a rolling dev-local version
                from components.java
            }
        }
    }
}

project(':web-app') {
    dependencies {
        runtime project(':rest-api')

        compile group: 'org.evelyn.library', name: 'authentication', version: "${libraryAuthenticationVersion}"
        compile group: 'org.evelyn.library', name: 'remote-configuration', version: "${libraryRemoteConfigurationVersion}"
    }
}

task localPublish {
    group = 'evelyn'
    dependsOn ':rest-api:publishToMavenLocal'
}

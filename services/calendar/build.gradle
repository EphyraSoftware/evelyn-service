subprojects {
    apply plugin: 'java'

    repositories {
        jcenter()
        maven {
            name = "Nexus"
            url = uri("https://nexus.evelyn.internal:443/repository/ephyra")
        }
        mavenLocal()
    }

    sourceCompatibility = 14
    targetCompatibility = 14
}

project.version = '0.1.0'

ext {
    springFrameworkVersion = '5.2.7.RELEASE'
    springSecurityVersion = '5.3.3.RELEASE'
    springBootVersion = '2.3.1.RELEASE'
    springRabbitVersion = '2.2.8.RELEASE'
    lombokVersion = '1.18.12'

    libraryAuthenticationVersion = '0.1.3-SNAPSHOT'
    libraryMessageModelsVersion = '0.1.0-SNAPSHOT'
}

project(":evelyn-service") {
    dependencies {
        implementation group: 'org.evelyn.library', name: 'message-models', version: "${libraryMessageModelsVersion}"

        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: "${springBootVersion}"

        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        compileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"

        // https://mvnrepository.com/artifact/org.springframework.amqp/spring-rabbit
        implementation group: 'org.springframework.amqp', name: 'spring-rabbit', version: "${springRabbitVersion}"

        // https://mvnrepository.com/artifact/org.springframework/spring-context-support
        implementation group: 'org.springframework', name: 'spring-context-support', version: "${springFrameworkVersion}"

        // https://mvnrepository.com/artifact/org.mnode.ical4j/ical4j
        implementation group: 'org.mnode.ical4j', name: 'ical4j', version: '3.0.11'

        // https://mvnrepository.com/artifact/javax.cache/cache-api
        implementation group: 'javax.cache', name: 'cache-api', version: '1.1.1'

        // https://mvnrepository.com/artifact/com.hazelcast/hazelcast
        runtime group: 'com.hazelcast', name: 'hazelcast', version: '3.12.5'

        // https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
        implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

        // https://mvnrepository.com/artifact/org.springframework/spring-webmvc
        compileOnly group: 'org.springframework', name: 'spring-webmvc', version: "${springFrameworkVersion}"

        testImplementation('org.junit.jupiter:junit-jupiter:5.5.2')
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

project(':rest-api') {
    apply plugin: 'maven-publish'

    group = 'org.evelyn.service'

    dependencies {
        implementation project(':evelyn-service')

        implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: "${springSecurityVersion}"
        implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: "${springSecurityVersion}"

        implementation group: 'org.evelyn.library', name: 'authentication', version: "${libraryAuthenticationVersion}"

        // https://mvnrepository.com/artifact/org.springframework/spring-webmvc
        compileOnly group: 'org.springframework', name: 'spring-webmvc', version: "${springFrameworkVersion}"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = "${rootProject.name}-rest-api"
                version = 'dev-local' // Publish a rolling dev-local version
                from components.java
            }
        }
    }
}

/*
 * ******************
 * Local JAR publishing.
 * ******************
 */
task localPublish {
    group = 'evelyn'
    description 'Publishes the core of the service to be used by the monolith server.'
    dependsOn ':rest-api:publishToMavenLocal'
}

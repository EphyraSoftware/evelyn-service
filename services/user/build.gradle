subprojects {
    apply plugin: 'java'

    repositories {
        jcenter()
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.springframework/spring-context
        compile group: 'org.springframework', name: 'spring-context', version: '5.0.4.RELEASE'

        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
    }
}

project(':services:user:evelyn-service') {
    dependencies {
        compile project(':services:user:api')
        compile project(':services:user:repository:api')
    }
}

if (gradle.ext.hasStorage) {
    project(":services:user:repository:$gradle.ext.storage") {
        dependencies {
            compile project(':services:user:repository:api')
        }
    }
}
else {
    project(":services:user:repository:mongo") {
        dependencies {
            compile project(':services:user:repository:api')
        }
    }

    project(":services:user:repository:mysql") {
        dependencies {
            compile project(':services:user:repository:api')
        }
    }
}

project(':services:user:rest-api') {
    dependencies {
        compile project(':services:user:api')
    }
}

project(':services:user:web-app') {
    dependencies {
        runtime project(':services:user:rest-api')
        runtime project(':services:user:evelyn-service')

        if (gradle.ext.hasStorage) {
            runtime project(":services:user:repository:$gradle.ext.storage")
        }
        else {
            runtime project(":services:user:repository:mongo")
        }
    }
}
